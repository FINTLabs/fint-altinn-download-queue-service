plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'groovy'
}

group = 'no.fint'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	jaxb
}

repositories {
	mavenCentral()
}

task genJaxb {
	ext.sourcesDir = "$buildDir/generated-sources/jaxb"
	ext.classesDir = "$buildDir/classes/jaxb"
	ext.schema = "$projectDir/src/main/resources/DownloadQueueExternalBasic.wsdl"

	outputs.dir classesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath
			mkdir(dir: sourcesDir)
			mkdir(dir: classesDir)

			xjc(destdir: sourcesDir, schema: schema,
					package: "no.altinn.downloadqueue.wsdl") {
				arg(value: "-wsdl")
				produces(dir: sourcesDir, includes: "**/*.java")
			}

			javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
					debugLevel: "lines,vars,source",
					classpath: configurations.jaxb.asPath) {
				src(path: sourcesDir)
				include(name: "**/*.java")
				include(name: "*.java")
			}

			copy(todir: classesDir) {
				fileset(dir: sourcesDir, erroronmissingdir: false) {
					exclude(name: "**/*.java")
				}
			}
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web-services'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	compile(files(genJaxb.classesDir).builtBy(genJaxb))
	jaxb 'com.sun.xml.bind:jaxb-xjc:2.1.7'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.ws:spring-ws-test'
	testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
	testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'
}
